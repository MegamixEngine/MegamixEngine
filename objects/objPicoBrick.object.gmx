<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>sprPicoBrick</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>10000</depth>
  <persistent>0</persistent>
  <parentName>prtEntity</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/*
    Pico bricks.
    Simply place any number of these in a room and they will become a boss fight.
    Set the "priority" variable in creation code to determine the order they will
    spawn.
    negative priority means the brick will never be used.
    If more than two bricks share a priority, they will appear in 
    a random order.
*/
event_inherited();

enemyDamageValue(objBusterShotCharged, 3);

respawnRange = -1; // set to -1 to make infinite
despawnRange = -1; // set to -1 to make infinite

canHit = false;
grav = 0;
blockCollision = false;
isSolid = true;
contactDamage = 5;

hpmax = 0;
hp = 0;

// @cc the number in order that the pico bricks go. Since they come in twos, there
// should be two bricks each that have the same priority number.
priority = 0;

// @cc if set to true, this will be the pico brick that settings such as music,
// item drops, etc will be read off of.
settingTransplant = false;

phase = 0;
phaseTimer = 0;
sectionLeft = 0;
sectionRight = 0;
sectionTop = 0;
sectionBottom = 0;

moveSpeed = .9 + (global.difficulty == DIFF_HARD * .75);

shiftVisible = 2;

// Ignore this. This is used for setting transplant
music = "Mega_Man_2.nsf";
musicType = "VGM";
musicTrackNumber = 17;
musicVolume = 0.8;
musicLoop = true;
musicLoopSecondsStart = 8.825;
musicLoopSecondsEnd = 61.575;

itemDrop = noone;
elementName = "";
elementScript = scrNoEffect;
elementCode = "";

useEndStageBehavior = true;
lockTransitions = true;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited();

xspeed = 0;
yspeed = 0;

if (entityCanStep())
{
    if (phase == 1)
    {
        if (isSolid)
        {
            // setup
            isSolid = false;
            
            var ax = x;
            if (image_xscale &lt; 0)
                ax -= 16;
            
            // place brick beneath
            instance_create(ax, y, objSolid);
            tile_add(tstMM2Wily2, 48, 48, 16, 16, ax, y, depth);
            visible = true;
            depth -= 1;
            exit;
        }
        
        // make sure partner exists:
        if (!instance_exists(partner))
        {
            with (objPicoControl)
                puyoBrickIndex += 2;
            instance_destroy();
            exit;
        }
        
        if (partner.y != y)
        {
            // unusual circumstance: nearby x and y; move apart
            if (((partner.x - x &lt; 0 &amp;&amp; image_xscale == -1)
                || (partner.x - x &gt; 0 &amp;&amp; image_xscale == 1))
                &amp;&amp; abs(partner.y - y) &lt; 48)
                x += image_xscale * moveSpeed;
            else
            {
                // move up/down to partner
                var yDir = sign(partner.y - y)
                    ;
                if (abs(partner.y - y) &lt; spd)
                    y = partner.y;
                else
                {
                    y += yDir * moveSpeed;
                }
            }
        }
        else
        {
            // move left/right to partner
            if (abs(partner.x - x) &gt; 1.5 * moveSpeed)
            {
                var xDir = sign(partner.x - x);
                x += xDir * moveSpeed;
            }
            else
            {
                // merge into Puyo Kun
                if (!instance_exists(objPicoKun))
                {
                    with (instance_create(x / 2 + partner.x / 2, y + 12, objPicoKun))
                    {
                        spd = other.spd;
                        respawn = false;
                        contactDamage = other.contactDamage;
                    }
                }
                
                // If on hard mode, create a bullet spread
                if (global.difficulty == DIFF_HARD)
                {
                    playSFX(sfxEnemyShootClassic);
                    
                    // or loop here to avoid copypasting code.
                    for (var i = -1; i &lt;= 1; i += 2)
                    {
                        with (instance_create(x, y, objEnemyBullet))
                        {
                            xspeed = other.i;
                            yspeed = -6;
                            grav = gravAccel;
                        }
                    }
                }
                
                // destroy the actual bricks
                with (partner)
                {
                    instance_destroy();
                }
                instance_destroy();
            }
        }
    }
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="20">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited();

// should destroy in case player comes back to room.
instance_destroy();
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="4">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited();

// determine if this section already has a control object
var controllerExists = false;

setSection(x, y, false);

with (objPicoControl)
{
    if (sectionLeft == other.sectionLeft &amp;&amp; sectionTop == other.sectionTop)
        controllerExists = true;
}

// create control object
if (!controllerExists)
{
    with (instance_create(x + 8, y + 8, objPicoControl))
    {
        sectionLeft = other.sectionLeft;
        sectionTop = other.sectionTop;
        
        // construct list of puyo bricks, sorted by appearance order.
        puyoBrickN = 0;
        puyoBrickIndex = 0;
        
        // construct sorted list of priorities:
        var puyoBrickPriorities;
        with (objPicoBrick)
        {
            setSection(x, y, false);
            if (other.sectionLeft == sectionLeft &amp;&amp; other.sectionTop == sectionTop)
            {
                if (priority &gt;= 0)
                {
                    puyoBrickPriorities[other.puyoBrickN] = priority;
                    other.puyoBrickList[other.puyoBrickN] = id;
                    other.puyoBrickN++;
                }
            }
            
            // If set to true, this pico brick will transfer all settings
            if (settingTransplant)
            {
                other.music = music;
                other.musicType = musicType;
                other.musicTrackNumber = musicTrackNumber;
                other.musicVolume = musicVolume;
                other.musicLoop = musicLoop;
                other.musicLoopSecondsStart = musicLoopSecondsStart;
                other.musicLoopSecondsEnd = musicLoopSecondsEnd;
                
                other.itemDrop = itemDrop;
                other.elementName = elementName;
                other.elementScript = elementScript;
                other.elementCode = elementCode;
                
                other.useEndStageBehavior = useEndStageBehavior;
                other.lockTransitions = lockTransitions;
            }
        }
        if (puyoBrickN &gt; 0)
            quickSortBy(puyoBrickList, puyoBrickPriorities);
        
        /// guarantees even set of puyo bricks
        puyoBrickN = floorTo(puyoBrickN, 2);
        if (puyoBrickN == 0)
            instance_destroy();
    }
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
