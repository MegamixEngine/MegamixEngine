<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>sprOptionsMenuBG</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>-2002</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>global.nextRoom = -1;
visible = 0;

if (!instance_number(object_index) &gt; 1)
{
    instance_destroy();
    visible = 0;
    exit;
}

cursorImgIndex = 0;
cursorImgSpd = 0.2;

arrowBlinkTimer = 20;
showArrow = true;

phase = 0;
option = 0;
selected = 0;
buffer = 0;

visibleOptions = 9;
optionOffset = 0;

verticalQuickScrollTimer = 25;
horizontalQuickScrollTimer = 25;

// Control settings
newControls = 0;
current = 'NONE';
press = 1;

// Option text
optionText[0] = 'CLOSE';
optionText[1] = 'CONFIGURE CONTROLS';
optionText[2] = 'CONFIGURE GAMEPAD';
optionText[3] = 'RESOLUTION';
optionText[4] = 'V-SYNC';
optionText[5] = 'MUSIC VOLUME';
optionText[6] = 'SOUND VOLUME';
optionText[7] = 'PLAYER COUNT';
optionText[8] = 'SHOW FPS';
optionText[9] = 'SHOW CONTROLLER';
optionText[10] = 'DAMAGE POPUP';
optionText[11] = 'CHARGE BAR';
optionText[12] = 'MM COLOR';
optionText[13] = 'MOVING TEXT';
optionText[14] = 'PICKUP EFFECT';
optionText[15] = 'ESC KEY BEHAVIOR';
optionText[16] = 'COLORED TEXT OVERLAYS';
optionText[17] = 'ITEM GRAPHICS';
optionText[18] = 'DEATH EFFECT';
optionText[19] = 'JUMP SOUND';
optionText[20] = 'TELEPORT SOUND';
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>buffer = 0;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// Open and close behavior
switch (phase)
{
    // Fading out
    case 0:
        if (objGlobalControl.fadeAlpha == 1)
        {
            global.nextRoom = 0;
            objGlobalControl.showhealth = 0;
            visible = 1;
            phase++;
        }
        break;
    
    // Fading in
    case 1:
        if (objGlobalControl.fadeAlpha == 0)
        {
            phase++;
        }
        break;
    
    // Doing menu selections
    case 2: // done elsewhere 
        break;
    
    // Fading out
    case 3:
        if (room == rmOptions)
        {
            var ID;
            if (global.nextRoom != rmTitleScreen)
            {
                saveLoadOptions(true);
            }
            global.nextRoom = rmTitleScreen;
        }
        else
        {
            global.nextRoom = -1;
            phase++;
        }
        break;
    
    // End fading out
    case 4:
        if (objGlobalControl.fadeAlpha == 1 &amp;&amp; global.nextRoom != 0)
        {
            global.nextRoom = 0;
            visible = 0;
        }
        else if (objGlobalControl.fadeAlpha == 0 &amp;&amp; global.nextRoom == 0)
        {
            global.frozen = true;
            instance_destroy();
        }
        break;
}
</string>
          </argument>
        </arguments>
      </action>
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// Most menu selections

if (instance_exists(objColoredTextOverlaysMenu))
{
    exit;
}

if (phase == 2 &amp;&amp; !selected)
{
    // var maxplayers = min(4+gamepad_get_device_count(),4);
    
    // left / right controlled selections
    dir = (global.keyRightPressed[0] || (global.keyRight[0] &amp;&amp; horizontalQuickScrollTimer &lt;= 0)) - (global.keyLeftPressed[0] || (global.keyLeft[0] &amp;&amp; horizontalQuickScrollTimer &lt;= 0));
    if (dir != 0)
    {
        switch (option)
        {
            // Screensize
            case 3:
                pre = global.screensize;
                setScreenSize(global.screensize + dir);
                if (global.screensize != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // V-Sync
            case 4:
                global.vsync = !global.vsync;
                playSFX(sfxMenuMove);
                break;
            
            // Music volume
            case 5:
                pre = global.musicvolume;
                global.musicvolume = max(0, min(100, global.musicvolume + 10 * dir));
                
                // adjust current music volume to reflect change
                switch (global.levelSongType)
                {
                    default:
                    case "OGG":
                        FMODInstanceSetVolume(global.songMemory, global.musicvolume * 0.01);
                        break;
                    case "VGM":
                        if (instance_exists(objMusicControl))
                        {
                            audio_sound_gain(objMusicControl.sound_index, global.levelVolume * (global.musicvolume * 0.01), 0);
                        }
                        break;
                }
                if (global.musicvolume != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Sound volume
            case 6:
                pre = global.soundvolume;
                global.soundvolume = max(0, min(100, global.soundvolume + 10 * dir));
                audio_master_gain(global.soundvolume * 0.01);
                if (global.soundvolume != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Players (co-op)
            case 7:
                pre = global.playerCount;
                global.playerCount = max(1, min(4, global.playerCount + dir));
                if (global.playerCount != pre)
                {
                    playSFX(sfxMenuMove);
                }
                if (global.playerCount &gt; pre)
                {
                    resetPlayerState(pre);
                    
                    // allow player to respawn:
                    global.respawnTimer[pre] = 0;
                }
                
                // delete removed players
                with (objMegaman)
                {
                    if (playerID &gt;= global.playerCount)
                    {
                        instance_destroy();
                    }
                }
                break;
            
            // FPS
            case 8:
                pre = global.showFPS;
                global.showFPS = min(max(0, global.showFPS + dir), 1);
                if (global.showFPS != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Controller Overlay
            case 9:
                pre = global.showControllerOverlay;
                global.showControllerOverlay = min(max(0, global.showControllerOverlay + dir), 2);
                if (global.showControllerOverlay != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Damage popup
            case 10:
                pre = global.damagePopup;
                global.damagePopup = min(max(0, global.damagePopup + dir), 1);
                if (global.damagePopup != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Charge Bar
            case 11:
                pre = global.chargeBar;
                global.chargeBar = min(max(0, global.chargeBar + dir), 1);
                if (global.chargeBar != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // MM Color
            case 12:
                pre = global.mmColor;
                global.mmColor = min(max(0, global.mmColor + dir), 1);
                if (global.mmColor != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // moving text effects
            case 13:
                pre = global.showMovingText;
                global.showMovingText = min(max(0, global.showMovingText + dir), 1);
                if (global.showMovingText != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Instant refills
            case 14:
                pre = global.healthEffect;
                global.healthEffect = min(max(0, global.healthEffect + dir), 1);
                if (global.healthEffect != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Escape key behavior
            case 15:
                pre = global.escapeBehavior;
                global.escapeBehavior = min(max(0, global.escapeBehavior - dir), 2);
                if (global.escapeBehavior != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Pickup Graphics
            case 17:
                pre = global.pickupGraphics;
                global.pickupGraphics = min(max(0, global.pickupGraphics + dir), 1);
                if (global.pickupGraphics != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Death Effect
            case 18:
                pre = global.deathEffect;
                global.deathEffect = min(max(0, global.deathEffect + dir), 1);
                if (global.deathEffect != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Jump Sound
            case 19:
                pre = global.jumpSound;
                global.jumpSound = min(max(0, global.jumpSound + dir), 1);
                if (global.jumpSound != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
            
            // Teleport Sound
            case 20:
                pre = global.teleportSound;
                global.teleportSound = min(max(0, global.teleportSound + dir), 1);
                if (global.teleportSound != pre)
                {
                    playSFX(sfxMenuMove);
                }
                break;
        }
    }
    
    // pause controlled selections
    var pausePressed = false;
    for (var i = 0; i &lt; global.playerCount; i++)
        if (global.keyPausePressed[i])
            pausePressed = i + 1;
    
    if (pausePressed)
    {
        switch (option)
        {
            // exit
            case 0:
                selected = true;
                phase++;
                playSFX(sfxMenuSelect);
                break;
            
            // set keyboard controls
            case 1:
                if (buffer)
                {
                    exit;
                }
                newControls = 1;
                selected = 1;
                current = 'LEFT';
                pID = pausePressed - 1;
                break;
            
            // set gamepad controls
            case 2:
                if (buffer)
                {
                    exit;
                }
                if (!objGlobalControl.joystick_connected)
                {
                    objGlobalControl.joystick_connected = gamepad_get_device_count() &gt; 0;
                }
                if (!objGlobalControl.joystick_connected)
                {
                    playSFX(sfxError);
                    exit;
                }
                newControls = 2;
                selected = 1;
                current = 'JUMP';
                pID = pausePressed - 1;
                break;
            
            // colored text overlays
            case 16:
                playSFX(sfxPause);
                instance_create(x, y, objColoredTextOverlaysMenu);
                break;
        }
    }
    
    // Up / down controls
    if (global.keyUpPressed[0] || (global.keyUp[0] &amp;&amp; verticalQuickScrollTimer &lt;= 0))
    {
        // up
        option -= 1;
        if (option &lt; 0)
        {
            // loop
            option = array_length_1d(optionText) - 1;
            optionOffset = array_length_1d(optionText) - visibleOptions;
        }
        else if (option &lt; optionOffset)
        {
            optionOffset--;
        }
        
        playSFX(sfxMenuMove);
    }
    else if (global.keyDownPressed[0] || (global.keyDown[0] &amp;&amp; verticalQuickScrollTimer &lt;= 0))
    {
        // down
        option += 1;
        if (option &gt;= array_length_1d(optionText))
        {
            // loop
            option = 0;
            optionOffset = 0;
        }
        else if (option &gt;= optionOffset + visibleOptions)
        {
            optionOffset++;
        }
        
        playSFX(sfxMenuMove);
    }
    
    // vertical quick scroll timer handling
    if (horizontalQuickScrollTimer == 25 // don't quick scroll vertically if doing it horizontally
    &amp;&amp; (global.keyUp[0] ^^ global.keyDown[0]) // only activate quick scroll if one button is being held
    &amp;&amp; !(option &lt;= 0 &amp;&amp; global.keyUp[0]) &amp;&amp; !(option &gt;= array_length_1d(optionText) - 1 &amp;&amp; global.keyDown[0])) // don't wrap around with quick scroll
    {
        if (verticalQuickScrollTimer &lt;= 0)
        {
            // slight pause between scrolls
            verticalQuickScrollTimer = 9;
        }
        
        verticalQuickScrollTimer--;
    }
    else
    {
        verticalQuickScrollTimer = 25; // &lt;-- time until quick scroll here
    }
    
    // horizontal quick scroll timer handling
    if (verticalQuickScrollTimer == 25 // don't quick scroll horizontally if doing it vertically
    &amp;&amp; (global.keyLeft[0] ^^ global.keyRight[0]))
    {
        if (horizontalQuickScrollTimer &lt;= 0)
        {
            // slight pause between scrolls
            horizontalQuickScrollTimer = 9;
        }
        
        horizontalQuickScrollTimer--;
    }
    else
    {
        horizontalQuickScrollTimer = 25; // &lt;-- time until quick scroll here
    }
}
</string>
          </argument>
        </arguments>
      </action>
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// Setup controls
if (!newControls)
{
    exit;
}

// control configuration
var key;
key = -1;

// control configuration
if (newControls == 1)
{
    if (keyboard_check_pressed(vk_anykey))
    {
        // illegal keys
        key = keyboard_key;
        switch (key)
        {
            case vk_f1:
            case vk_f2:
            case vk_f3:
            case vk_f4:
            case vk_f5:
            case vk_f6:
            case vk_f7:
            case vk_f8:
            case vk_f9:
            case vk_f10:
            case vk_f11:
            case vk_f12:
            case vk_escape:
            case vk_alt:
            case vk_nokey:
            case vk_printscreen:
                exit;
        }
    }
    else
    {
        // no keys pressed
        press = 0;
    }
    
    if (key != -1 &amp;&amp; !press)
    {
        // assign key
        press = 1;
        switch (current)
        {
            case 'LEFT':
                global.leftKey[pID] = key;
                current = 'RIGHT';
                break;
            case 'RIGHT':
                global.rightKey[pID] = key;
                current = 'UP';
                break;
            case 'UP':
                global.upKey[pID] = key;
                current = 'DOWN';
                break;
            case 'DOWN':
                global.downKey[pID] = key;
                current = 'JUMP';
                break;
            case 'JUMP':
                global.jumpKey[pID] = key;
                current = 'SHOOT';
                break;
            case 'SHOOT':
                global.shootKey[pID] = key;
                current = 'SLIDE';
                break;
            case 'SLIDE':
                global.slideKey[pID] = key;
                current = 'PREV WPN';
                break;
            case 'PREV WPN':
                global.weaponSwitchLeftKey[pID] = key;
                current = 'NEXT WPN';
                break;
            case 'NEXT WPN':
                global.weaponSwitchRightKey[pID] = key;
                current = 'PAUSE';
                break;
            case 'PAUSE':
                global.pauseKey[pID] = key;
                current = 'LEFT';
                pID += 1;
                if (pID &gt;= global.playerCount)
                {
                    selected = 0;
                    buffer = 1;
                    alarm[0] = 10;
                    newControls = 0;
                }
                
                break;
        }
        
        playSFX(sfxMenuMove);
    }
}

// gamepad configuration
if (newControls == 2)
{
    if (!objGlobalControl.joystick_connected)
    {
        newControls = 0;
        selected = 0;
        press = 1;
        buffer = 1;
        alarm[0] = 10;
        exit;
    }
    
    var i, b;
    var jID = pID;
    if (!gamepad_is_connected(jID))
        jID += 4 - objGlobalControl.xinputDeviceCount;
    b = gamepad_button_count(jID);
    
    for (i = 0; i &lt; b; i += 1)
    {
        if (gamepad_button_check(jID, i))
        {
            key = i;
            break;
        }
    }
    if (key == -1)
    {
        press = 0;
    }
    if (key != -1 &amp;&amp; !press)
    {
        press = 1;
        switch (current)
        {
            case 'JUMP':
                global.joystick_jumpKey[pID] = key;
                current = 'SHOOT';
                break;
            case 'SHOOT':
                global.joystick_shootKey[pID] = key;
                current = 'SLIDE';
                break;
            case 'SLIDE':
                global.joystick_slideKey[pID] = key;
                current = 'PREV WPN';
                break;
            case 'PREVIOUS WPN':
                global.joystick_weaponSwitchLeftKey[pID] = key;
                current = 'NEXT WPN';
                break;
            case 'NEXT WPN':
                global.joystick_weaponSwitchRightKey[pID] = key;
                current = 'PAUSE';
                break;
            case 'PAUSE':
                global.joystick_pauseKey[pID] = key;
                current = 'JUMP';
                pID += 1;
                if (pID &gt;= 1 &amp;&amp; gamepad_get_device_count() &gt; pID) // global.playerCount
                {
                    selected = 0;
                    buffer = 1;
                    alarm[0] = 10;
                    newControls = 0;
                }
                break;
        }
        
        playSFX(sfxMenuMove);
    }
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>draw_set_color(c_white);

// center display on non-standard resolutions
drawXPosition = view_xview[0] + view_wview[0] / 2 - 128;
drawYPosition = view_yview[0] + view_hview[0] / 2 - 112;

// draw menu bg
draw_sprite(sprite_index, 0, drawXPosition, drawYPosition);

// animate cursor
cursorImgIndex += cursorImgSpd;
if (cursorImgIndex &gt;= 5)
{
    cursorImgIndex -= 5;
}

// draw cursor
draw_sprite(sprOptionsCursor, cursorImgIndex, drawXPosition + 34, drawYPosition + 52 + (option) * 14 - (optionOffset) * 14);

// animate arrows
arrowBlinkTimer -= 1;
if (arrowBlinkTimer == 0)
{
    arrowBlinkTimer = 20;
    showArrow = !showArrow;
}

// draw arrows
draw_set_halign(fa_middle);
draw_set_valign(fa_middle);

if (optionOffset == 0)
{
    draw_text(drawXPosition + 128, drawYPosition + 42, "- OPTIONS -");
}
else if (showArrow)
{
    draw_sprite(sprArrow, 2, drawXPosition + 128, drawYPosition + 42);
}

if (optionOffset == array_length_1d(optionText) - visibleOptions)
{
    draw_text(drawXPosition + 128, drawYPosition + 182, "- OPTIONS -");
}
else if (showArrow)
{
    draw_sprite(sprArrow, 3, drawXPosition + 128, drawYPosition + 182);
}

// draw options
draw_set_halign(fa_left);
draw_set_valign(fa_top);
for (var j = 0; j &lt; visibleOptions; j++)
{
    draw_text_ext(drawXPosition + 48, drawYPosition + 52 + j * 14, optionText[optionOffset + j], 14, 256);
}

// draw current option status'
opt = '';
for (var j = 0; j &lt; visibleOptions; j++)
{
    switch (optionOffset + j)
    {
        // Resolution
        case 3:
            if (!window_get_fullscreen())
            {
                opt += string(global.screensize) + 'X';
            }
            else
            {
                opt += 'FULL';
            }
            
            break;
        
        // V-Sync
        case 4:
            if (global.vsync)
            {
                opt += 'ON';
            }
            else
            {
                opt += 'OFF';
            }
            
            break;
        
        // Music volume
        case 5:
            opt += string(global.musicvolume) + '%';
            
            break;
        
        // Sound volume
        case 6:
            opt += string(global.soundvolume) + '%';
            
            break;
        
        // Co-op
        case 7:
            opt += string(global.playerCount);
            
            break;
        
        // FPS dispaly
        case 8:
            if (global.showFPS == 1)
            {
                opt += 'ON';
            }
            else
            {
                opt += 'OFF';
            }
            
            break;
        
        // controller overlay
        case 9:
            switch (global.showControllerOverlay)
            {
                case 0:
                    opt += 'OFF';
                    break;
                case 1:
                    opt += 'WIDE';
                    break;
                case 2:
                    opt += 'THIN';
                    break;
            }
            
            break;
        
        // damage popup
        case 10:
            if (global.damagePopup == 1)
            {
                opt += 'ON';
            }
            else
            {
                opt += 'OFF';
            }
            
            break;
        
        // charge bar
        case 11:
            if (global.chargeBar == 1)
            {
                opt += 'ON';
            }
            else
            {
                opt += 'OFF';
            }
            
            break;
        
        // mega man colors
        case 12:
            if (global.mmColor)
            {
                opt += 'MM9/MM10';
            }
            else
            {
                opt += 'MM1-MM6';
            }
            
            break;
        
        // moving text effects
        case 13:
            if (global.showMovingText)
            {
                opt += 'ON';
            }
            else
            {
                opt += 'OFF';
            }
            
            break;
        
        // health effect
        case 14:
            switch (global.healthEffect)
            {
                case 0:
                    opt += 'FILL';
                    break;
                case 1:
                    opt += 'INSTANT';
                    break;
            }
            
            break;
        
        // esc key function
        case 15:
            switch (global.escapeBehavior)
            {
                case 0:
                    opt += 'QUIT';
                    break;
                case 1:
                    opt += 'PAUSE';
                    break;
                case 2:
                    opt += 'N/A';
                    break;
            }
            
            break;
        
        // Pickup graphics
        case 17:
            if (global.pickupGraphics)
            {
                opt += 'MM1';
            }
            else
            {
                opt += 'NEW';
            }
            break;
        
        // Death effect
        case 18:
            if (global.deathEffect)
            {
                opt += 'OLD';
            }
            else
            {
                opt += 'NEW';
            }
            break;
        
        // Jump sound
        case 19:
            if (global.jumpSound)
            {
                opt += 'OLD';
            }
            else
            {
                opt += 'NEW';
            }
            break;
        
        // Teleport sound
        case 20:
            if (global.teleportSound)
            {
                opt += 'OLD';
            }
            else
            {
                opt += 'NEW';
            }
            break;
    }
    
    // next line
    opt += '#';
}

draw_set_halign(fa_right);
draw_text_ext(drawXPosition + 224, drawYPosition + 52, opt, 14, 256);


// draw configure controls popup
if (newControls)
{
    draw_sprite(sprControlconfigureScreen, 0, drawXPosition + 64, drawYPosition + 64);
    draw_set_halign(fa_center);
    draw_text(drawXPosition + 128, drawYPosition + 88, 'PRESS KEY FOR');
    var playerMod = "";
    playerMod = " (P" + string(pID + 1) + ")";
    draw_text(drawXPosition + 128, drawYPosition + 104, current + playerMod);
}

// colored text overlays
if (global.showColoredTextOverlays)
{
    draw_set_color(make_color_rgb(global.coloredTextOverlaysRed, global.coloredTextOverlaysGreen, global.coloredTextOverlaysBlue));
    draw_set_alpha(global.coloredTextOverlaysOpacity / 255);
    
    draw_rectangle(drawXPosition + 28, drawYPosition + 36, drawXPosition + view_wview[0] - 28, drawYPosition + view_hview[0] - 36, false);
    
    draw_set_color(c_white);
    draw_set_alpha(1);
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
