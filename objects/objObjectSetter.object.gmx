<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>sprObjectSetter</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>9999999</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// Autotiler for tilesets, place the whole tileset image somewhere in the level and place this
// object on the top left tile of the tileset, then place one of the supported objects of your choice over the tiles.
// If the object is flipped vertically the object setter will use the tile asociated to it as a chunk of tiles with the whole tileset
//preventing any weird behavior caused by using the object setter on an incomplete chunk of tiles

useTileAsTileset = image_yscale&lt;0;

if(useTileAsTileset)
{
    image_yscale=1;
    y-=16;
}

// only one autotiler runs this code:
if (id == object_index.id)
{
    var beginTime = get_timer();
    print("Autotile begin (instance " + string(id) + ")", WL_VERBOSE);
    
    // learn list of layers
    print("Determining layer set...", WL_VERBOSE);
    var preplace_time = get_timer();
    global.tileLayersN = 0;
    var tid_all = tile_get_ids();
    var tid_all_n = array_length_1d(tid_all);
    if (tid_all_n &lt;= 0)
        exit;
    var layers;
    layers[0] = 0;
    global.tileLayers = layers;
    for (var i = 0; i &lt; tid_all_n; i++)
    {
        var d = tile_get_depth(tid_all[i]);
        if (indexOf(global.tileLayers, d) == -1)
        {
            global.tileLayers[global.tileLayersN++] = d;
        }
    }
    
    print("Sorting layer set", WL_VERBOSE);
    
    // sort layer list
    quickSort(global.tileLayers, 0, global.tileLayersN);
    
    print("Layers found: " + string(global.tileLayers), WL_VERBOSE);
    
    // map from (layer, bg) -&gt; (automapper id)
    for (var i = 0; i &lt; global.tileLayersN; i++)
        global.automapper[i] = ds_map_create();
    
    // set of instances which were placed during the autotiler process
    // maps (instance id) -&gt; (true)
    var placed_instances = ds_map_create();
    
    // each object scans for a key and parses it
    with (object_index)
    {
        event_user(0);
    }
    
    preplace_time = get_timer() - preplace_time;
    print("Iterating through all tiles in all layers...", WL_VERBOSE);
    var place_time = get_timer();
    
    for (var layerIndex = 0; layerIndex &lt; global.tileLayersN; layerIndex++)
    {
        // list each tile on each layer
        var layer = global.tileLayers[layerIndex];
        var tid = tile_get_ids_at_depth(layer);
        var tid_n = array_length_1d(tid);
        
        print("Layer " + string(layer) + "...", WL_VERBOSE);
        print("  Splitting tiles...", WL_VERBOSE);
        var tile_split_time = get_timer();
        
        // split tiles into 16x16
        for (var i = 0; i &lt; tid_n; i++)
        {
            splitTile(tid[i], grid, grid);
        }
        print("  (" + string((get_timer() - tile_split_time) / 1000) + " ms)");
        print("  Placing instances...", WL_VERBOSE);
        
        // look at every tile in room and place objects
        var tid = tile_get_ids_at_depth(layer);
        var tid_n = array_length_1d(tid);
        var instance_placed_n = 0; // this variable is purely to help debugging
        var instance_place_time = get_timer();
        
        for (var i = 0; i &lt; tid_n; i++)
        {
            // determine which objObjectSetter has the associated tileset
            var bg = tile_get_background(tid[i]);
            var automapperID = ds_map_find_value(global.automapper[layerIndex], bg);
            
            if (is_undefined(automapperID))
            {
                continue;
            }
            
            with (automapperID)
            {
                // figure out which object to place according to tileset key (which is determined in user defined 0)
                var key_lookup_x = clamp(floor(tile_get_left(tid[i]) / grid), 0, floor(key_width / grid - 1));
                var key_lookup_y = clamp(floor(tile_get_top(tid[i]) / grid), 0, floor(key_height / grid - 1));
                var obj_id = coord[# key_lookup_x, key_lookup_y];
                if (obj_id &gt;= 0)
                {
                    with (instance_create(floor(tile_get_x(tid[i]) / grid) * grid, floor(tile_get_y(tid[i]) / grid) * grid, obj_id))
                    {
                        placed_instances[? id] = true;
                        isTile = true;
                        visible = false;
                        instance_placed_n++;
                    }
                }
            }
        }
        
        print("  Placed " + string(instance_placed_n) + " instances.", WL_VERBOSE);
        print("  (" + string((get_timer() - instance_place_time) / 1000) + " ms)");
    }
    place_time = get_timer() - place_time;
    
    // clean up resources
    print("Cleaning up data structures...", WL_VERBOSE);
    var free_time = get_timer();
    print("  Freeing legend grids...", WL_VERBOSE);
    with (object_index)
    {
        ds_grid_destroy(coord);
    }
    
    print("  Destroying secondary autotilers...", WL_VERBOSE);
    with (object_index)
    {
        if (id != other.id)
        {
            instance_destroy();
        }
    }
    
    print("  Freeing layer -&gt; autotiler map", WL_VERBOSE);
    for (var i = 0; i &lt; global.tileLayersN; i++)
        ds_map_destroy(global.automapper[i]);
    
    print("  Freeing placed instances map", WL_VERBOSE);
    ds_map_destroy(placed_instances);
    
    print("Finished object setting. Overall time taken: " + string((get_timer() - beginTime) / 1000) + " ms.", WL_VERBOSE);
    instance_destroy();
    
    free_time = get_timer() - free_time;
    print("  Pre-processing: " + string(preplace_time / 1000) + " ms");
    print("  tile placing: " + string(place_time / 1000) + " ms");
    print("  Freeing resources: " + string(free_time / 1000) + " ms");
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="10">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// parse key
var key = -1;

print("Parsing legend (instance " + string(id) + ")", WL_VERBOSE);
print("  Searching for legend...", WL_VERBOSE);

// search for key until one is found
for (var i = global.tileLayersN - 1; i &gt;= 0; i--)
{
    layer = global.tileLayers[i];
    layerIndex = i;
    key = tile_layer_find(layer, x + 8, y + 8);
    if (key != -1)
    {
        break;
    }
}

ERR_HEAD = "Autotile Error: ";

if (key == -1)
{
    printErr(ERR_HEAD + "Cannot find background key on any layer (at x = " + string(x) + ", y = " + string(y) + ")");
    exit;
}

// found a key and layer

grid = 16;


key_bg = tile_get_background(key);

if(!useTileAsTileset)
{
    key_width = tile_get_width(key);
    key_height = tile_get_height(key);
    key_left = round(tile_get_left(key) / grid);
    key_top = round(tile_get_top(key) / grid);
    key_x = tile_get_x(key) + grid / 2;
    key_y = tile_get_y(key) + grid / 2;
}
else
{
    key_width = background_get_width(key_bg);
    key_height = background_get_height(key_bg);
    key_left = 0;
    key_top = 0;
    key_x = tile_get_x(key) + grid / 2;
    key_y = tile_get_y(key) + grid / 2;
}

tile_delete(key);

print("  Legend found! " + background_get_name(key_bg) + " on layer " + string(layer), WL_VERBOSE);
print("  Scanning legend...", WL_VERBOSE);

ds_map_replace(global.automapper[layerIndex], key_bg, id);

var coordW = ceil(background_get_width(key_bg) / grid);
var coordH = ceil(background_get_height(key_bg) / grid)

;
coord = ds_grid_create(coordW, coordH);

for (var i = 0; i &lt; coordW; i++)
{
    for (var j = 0; j &lt; coordH; j++)
    {
        coord[# i, j] = -1;
    }
}

// objects that can be autotiled
// make sure descendants are lower down in this list than their ancestors
obj[0] = objSolid;
obj[1] = objIce;
obj[2] = objTopSolid;
obj[3] = objLadder;
obj[4] = objSpike;
obj[5] = objWater;
obj[6] = objBossBarrier;
obj[7] = objBossDoor;
obj[8] = objBossDoorVertical;
obj[9] = objLineHorizontalDrop;
obj[10] = objStandSolid;
obj[11] = objSlopeR;
obj[12] = objSlopeL;
obj[13] = objSlopeRLong;
obj[14] = objSlopeLLong;
obj[15] = objDamageSpike;
obj[16] = objLiftRail;
obj[17] = objLiftHole;
obj[18] = objLiftEnd;
obj[19] = objLineVertical;
obj[20] = objLineHorizontal;
obj[21] = objLineTopLeft;
obj[22] = objLineTopRight;
obj[23] = objLineBottomLeft;
obj[24] = objLineBottomRight;
obj[25] = objLineVerticalDrop;
obj[26] = objCossackSnow;
obj_n = array_length_1d(obj);

// scan key for objects
for (var _x = 0; _x &lt; key_width; _x += grid)
{
    for (var _y = 0; _y &lt; key_height; _y += grid)
    {
        obj_found = 0;
        
        for (var i = 0; i &lt; obj_n; i++) // find object located at this point
        {
            if (position_meeting(key_x + _x, key_y + _y, obj[i]))
            {
                obj_found = obj[i];
            }
        }
        
        if (obj_found) // object found - add to list
        {
            coord[# key_left + _x / grid, key_top + _y / grid] = obj_found;
        }
    }
}

print("  Scan complete.", WL_VERBOSE);
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
