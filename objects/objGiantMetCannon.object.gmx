<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>sprGiantMetCannonPreview</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>5</depth>
  <persistent>0</persistent>
  <parentName>prtMiniBoss</parentName>
  <maskName>mskGiantMetCannon</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// A Giant Met manning a giant cannon (egads, you don't say?!). It can fire either a large
// shot, or a homing missile from the back.

// Creation code (all optional):

event_inherited();
respawn = true;
doesIntro = false;
healthpointsStart = 16;
healthpoints = healthpointsStart;
contactDamage = 0;
blockCollision = 0;
grav = 0;
isSolid = true;

// @cc - Determines if it can shoot homing missiles AND cannon shots.
randomMissiles = false;

// @cc - Use this to make it fire ONLY cannon shots (0) or homing missiles (1)
// THIS WILL NOT WORK IF randomMissiles IS SET TO TRUE
shotType = 0;

// Enemy specific code
image_speed = 0;
image_index = 0;
curYOffset = -24;
yOffsetMax = -24;
cFrame = 0;

// animation offsets
spriteOffset = 8;
bottomPart = y + 48;

phase = 1;
oldPhase = 1;

attackTimer = 0;
attackTimerMax = 246;

delayTimer = 0;
delayTimerMax = 32;

missile1 = noone;
missile2 = noone;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited();
if (entityCanStep())
{
    attackTimer++;
    
    // to stop the met cannon from crushing a player underneath them, we change its mask
    switch (curYOffset)
    {
        case -20:
            mask_index = mskGiantMetCannon;
            break;
        case -16:
            mask_index = mskGiantMetCannon2;
            break;
        case -8:
            mask_index = mskGiantMetCannon3;
            break;
    }
    
    switch (phase)
    {
        case 0: // move up 
            if (curYOffset &gt; yOffsetMax)
            {
                yspeed = 0;
                curYOffset -= 0.25;
                yspeed = -0.25;
            }
            if (curYOffset == yOffsetMax)
            {
                yspeed = 0;
                curYOffset = yOffsetMax;
                phase = 1;
            }
            break;
        case 1:
        case 3: // delay movement 
            delayTimer++;
            if (delayTimer == delayTimerMax)
            {
                delayTimer = 0;
                phase++;
            }
            break;
        case 2: // move down 
            if (curYOffset &lt; 0)
            {
                curYOffset += 0.25;
                yspeed = 0.25;
            }
            if (curYOffset == 0)
            {
                yspeed = 0;
                curYOffset = 0;
                phase = 3;
            }
            break;
        case 4:
            phase = 0;
            break;
        case 5: // fire projectile 
            {
                if (shotType == 0) //Fire Cannonball
                {
                    switch (attackTimer)
                    {
                        case 1: // animation setup 
                            cFrame = 1;
                            break;
                        case 32:
                            cFrame = 3;
                            with (instance_create(x + 36 * image_xscale, y - 3, objGiantMetCannonShot))
                            {
                                image_xscale = other.image_xscale;
                            }
                            break;
                        case 38:
                            cFrame = 2;
                            break;
                        case 48:
                            cFrame = 0;
                            break;
                        case 64: // finish firing and return to previous phase. 
                            attackTimer = 0;
                            phase = oldPhase;
                            break;
                    }
                }
                else //Fire Missiles
                {
                    switch (attackTimer)
                    {
                        case 1: // animation setup 
                            cFrame = 1;
                            break;
                        case 32:
                            cFrame = 0;
                            if (!instance_exists(missile1))
                            {
                                with (instance_create(x - 27 * image_xscale, y - 20, objGunnerJoeMissile))
                                {
                                    other.missile1 = id;
                                    parent = other.id;
                                    despawnRange = -1;
                                    if (image_index == -1)
                                    {
                                        image_index = 1;
                                        direction = 135;
                                    }
                                    else
                                    {
                                        image_index = 3;
                                        direction = 45;
                                    }
                                    playSFX(sfxMissileLaunch);
                                }
                            }
                            if (!instance_exists(missile2))
                            {
                                with (instance_create(x - 27 * image_xscale, y - 20, objGunnerJoeMissile))
                                {
                                    other.missile2 = id;
                                    parent = other.id;
                                    despawnRange = -1;
                                    if (image_index == -1)
                                    {
                                        image_index = 0;
                                        direction = 180;
                                    }
                                    else
                                    {
                                        image_index = 4;
                                        direction = 0;
                                    }
                                    if (instance_exists(target))
                                    {
                                        direction = point_direction(x, y, target.x, target.y);
                                    }
                                    playSFX(sfxMissileLaunch);
                                }
                            }
                            break;
                        case 38: // finish firing and return to previous phase. 
                            attackTimer = 0;
                            phase = oldPhase;
                            break;
                    }
                }
            }
    }
    
    if (attackTimer == attackTimerMax) // start fire projectile phase
    {
        yspeed = 0;
        attackTimer = 0;
        oldPhase = phase;
        phase = 5;
        if (randomMissiles)
        {
            shotType = choose(0,1);
        }
    }
}
else if (!insideView())
{
    image_index = 0;
    y = ystart;
    x = xstart;
    attackTimer = 0;
    delayTimer = 0;
    phase = 1;
    curYOffset = -24;
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="21">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if (!(bboxGetYCenterObject(other.id) &lt; y - 20) || cFrame == 1 || cFrame == 3) //24
{
    other.guardCancel = 1;
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="20">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited();
with (missile1)
{
    instance_create(bboxGetXCenter(),bboxGetYCenter(),objExplosion);
    instance_destroy();
}
with (missile2)
{
    instance_create(bboxGetXCenter(),bboxGetYCenter(),objExplosion);
    instance_destroy();
}
with (objGiantMetCannonShot)
{
    instance_create(bboxGetXCenter(),bboxGetYCenter(),objExplosion);
    instance_destroy();
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// handles stuff differently than the Puny Regular Enemies....
if (introTimer &gt; 0 &amp;&amp; doesIntro)
{
    draw_sprite_ext(introSprite, 2 - (min(2, min(introTimer, 15) / 5)), x,
        y - max(introTimer - 10, 0) * image_yscale, image_xscale, image_yscale, 0, c_white, 1);
}
else
{
    // this debug message should be left in until
    // the spawn system stops breaking every week.
    
    if (spawned == -1)
    {
        show_debug_message(object_get_name(object_index) + " drawn without having ever spawned!");
    }
    if ((ceil(iFrames / 2) mod 4) || !iFrames)
    {
        if ((ceil(iFrames / 2) mod 2) || (iceTimer &gt; 0))
        {
            var flashcol = c_white;
            if (iceTimer &gt; 0)
            {
                flashcol = make_color_rgb(0, 120, 255);
            }
            
            d3d_set_fog(true, flashcol, 0, 0);
            
            for (var i = 0; i &lt; ceil(abs(curYOffset) / 8); i++)
            {
                draw_sprite_ext(sprGiantMetCannonMiddle, i, round(x)+1, round(y) + spriteOffset + (i * 8), image_xscale, image_yscale, image_angle, image_blend, image_alpha);
            }
            draw_sprite_ext(sprGiantMetCannonBottom, 0, round(x)+2, bottomPart, image_xscale, image_yscale, image_angle, image_blend, image_alpha);
            draw_sprite_ext(sprGiantMetCannonTop, cFrame, round(x), round(y) + spriteOffset, image_xscale, image_yscale, image_angle, image_blend, image_alpha);
            d3d_set_fog(false, 0, 0, 0);
            
            if (iceTimer &gt; 0)
            {
                draw_set_blend_mode(bm_add);
                
                for (var i = 0; i &lt; ceil(abs(curYOffset) / 8); i++)
                {
                    draw_sprite_ext(sprGiantMetCannonMiddle, i, round(x)+1, round(y) + spriteOffset + (i * 8), image_xscale, image_yscale, image_angle, image_blend, image_alpha);
                }
                draw_sprite_ext(sprGiantMetCannonBottom, 0, round(x)+2, bottomPart, image_xscale, image_yscale, image_angle, image_blend, image_alpha);
                draw_sprite_ext(sprGiantMetCannonTop, cFrame, round(x), round(y) + spriteOffset, image_xscale, image_yscale, image_angle, image_blend, image_alpha);
                draw_set_blend_mode(bm_normal);
            }
        }
        else
        {
            for (var i = 0; i &lt; ceil(abs(curYOffset) / 8); i++)
            {
                draw_sprite_ext(sprGiantMetCannonMiddle, i, round(x)+1, round(y) + spriteOffset + (i * 8), image_xscale, image_yscale, image_angle, image_blend, image_alpha);
            }
            draw_sprite_ext(sprGiantMetCannonBottom, 0, round(x)+2, bottomPart, image_xscale, image_yscale, image_angle, image_blend, image_alpha);
            draw_sprite_ext(sprGiantMetCannonTop, cFrame, round(x), round(y) + spriteOffset, image_xscale, image_yscale, image_angle, image_blend, image_alpha);
        }
    }
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
