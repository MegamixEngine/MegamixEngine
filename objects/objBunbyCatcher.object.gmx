<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>sprBunbyCatcher</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>-2</depth>
  <persistent>0</persistent>
  <parentName>prtEntity</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// An infamous enemy from MM9 that will stay invisible, then pop up and drop down when approached.
// If it grabs Mega Man, it will carry him very quickly until it feels like stopping or Mega Man hits a wall.

// Creation code (all optional):
// col - color. 0 = orange; 1 = pink
// stopX = X coordinate to stop carrying MM at. If not set, MM will be carried on until he hits a wall.
// catcherSpeed = xspeed for when the Bunby Catcher starts taking off after grabbing MM. Default 4

// NOTE: Its carrying direction relies on what image_xscale you give it in the editor.

event_inherited();

respawn = true;

healthpointsStart = 1;
healthpoints = healthpointsStart;
contactDamage = 0;

blockCollision = 0;
grav = 0;

// Enemy specific code
despawnRange = 32;
respawnRange = 32;

poppedUp = false;
stopTimer = -1;

othersGrabbed = false;

grabbedPlayer = false;
myPlayer = noone;

animTimer = 0;
image_speed = 0;
imageOffset = 0;

stopX = noone;
catcherSpeed = 4;

leaveTime = 120;
leaving = false;

xOffset = 0;
yOffset = 14;

col = 0; // 0 = orange; 1 = pink
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited();

if (entityCanStep())
{
    // This is to be on the safe side. A lot of crashes have occurred due to not checking if target exists.
    if (instance_exists(target))
    {
        // Check if the target (nearest player) is close.
        if (collision_rectangle(x - 16, y - 224, x + 16, y + 224, target, false,
            true) &amp;&amp; !poppedUp)
        {
            poppedUp = true;
            yspeed = 5.5;
            y = view_yview[0];
        }
        
        // Grab player
        if (collision_rectangle(x - 8, y + 6, x + 8, y + 15, target, false, true) &amp;&amp; !grabbedPlayer &amp;&amp; !leaving)
        {
            // Making sure other Bunby Catchers haven't grabbed Mega Man.
            othersGrabbed = false;
            with (objBunbyCatcher)
            {
                if (grabbedPlayer &amp;&amp; myPlayer == other.target &amp;&amp; !leaving)
                {
                    other.othersGrabbed = true;
                }
            }
            if (!othersGrabbed)
            {
                grabbedPlayer = true;
                myPlayer = target;
                playSFX(sfxBunbyCatcher);
                yspeed = 0;
            }
        }
    }
    
    if (grabbedPlayer &amp;&amp; instance_exists(myPlayer))
    {
        // lock controls
        global.keyLeft[myPlayer.playerID] = false;
        global.keyRight[myPlayer.playerID] = false;
        global.keyUp[myPlayer.playerID] = false;
        global.keyDown[myPlayer.playerID] = false;
        
        global.keyLeftPressed[myPlayer.playerID] = false;
        global.keyRightPressed[myPlayer.playerID] = false;
        global.keyUpPressed[myPlayer.playerID] = false;
        global.keyDownPressed[myPlayer.playerID] = false;
        
        // yOffset
        if (myPlayer.ground)
        {
            yOffset = 12;
        }
        else
        {
            yOffset = 16;
        }
        
        // xOffset
        xOffset = -myPlayer.image_xscale;
        
        // start gaining V E L O C I T Y
        if (abs(xspeed) &lt; catcherSpeed)
        {
            xspeed += 0.15 * image_xscale;
        }
        
        if ((abs(xspeed) &gt;= catcherSpeed) &amp;&amp; stopTimer == -1)
        {
            xspeed = sign(xspeed) * catcherSpeed;
            stopTimer = 0;
        }
        
        // timer for flying away
        if (stopTimer &gt;= 0)
        {
            stopTimer += 1;
        }
        
        if (((stopTimer &gt;= leaveTime) || (stopX != noone)) &amp;&amp; !leaving)
        {
            if ((stopX != noone &amp;&amp; ((image_xscale == 1 &amp;&amp; x &gt; stopX) || (image_xscale == -1 &amp;&amp; x &lt; stopX))) || (stopTimer &gt;= leaveTime))
            {
                leaving = true;
                grabbedPlayer = false;
            }
        }
        
        // update player position
        with (myPlayer)
        {
            shiftObject(other.xspeed - other.image_xscale, 0, 1);
        }
        x = myPlayer.x - xOffset;
        y = ceil(myPlayer.y - yOffset);
    }
    
    if (myPlayer != noone)
    {
        if (!instance_exists(myPlayer))
        {
            leaving = true;
            grabbedPlayer = false;
        }
    }
    
    // fly away like a coward
    if (leaving)
    {
        yspeed -= gravAccel;
        xspeed = 0;
    }
    
    // Animation
    animTimer += 1;
    if (animTimer == 2)
    {
        animTimer = 0;
        imageOffset = !imageOffset;
    }
}
else if (dead)
{
    poppedUp = false;
    stopTimer = -1;
    grabbedPlayer = false;
    myPlayer = noone;
    leaving = false;
    yspeed = 0;
    xspeed = 0;
}

image_index = (imageOffset + (grabbedPlayer * 2)) + (col * 4);
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="21">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// This enemy is invincible, so just do this to reflect bullets.

// The above comment is a liar.

if (poppedUp)
{
    other.guardCancel = 0;
}
else
{
    other.guardCancel = 2;
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if (!poppedUp)
{
    exit;
}

event_inherited();
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
